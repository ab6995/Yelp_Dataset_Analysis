business_data = 
	LOAD 'yelp_academic_data_business.json'
	USING JsonLoader(
    	'business_id:chararray,
        name:chararray,
        neighborhood:chararray,
        address:chararray,
        city:chararray, 
        state:chararray,
        postal_code:chararray,
        latitude:float,
        longitude:float,
        stars:float,
        review_count:int,
        is_open:int,
        attributes:bag{a:tuple(a:chararray)},
        categories:bag{a:tuple(a:chararray)},
        hours:bag{a:tuple(a:chararray)},
        type:chararray'
    );

user_data = 
	LOAD 'yelp_academic_data_user.json'
	USING JsonLoader(
		'user_id:chararray,
		name:chararray,
		review_count:int,
		yelping_since:chararray,
        friends:bag{a:tuple(a:chararray)},
        useful:int,
        funny:int,
        cool:int,
        fans:int,
        elite:bag{a:tuple(a:int)},
        average_stars:float,
        compliment_hot:int,
        compliment_more:int,
        compliment_profile:int,
        compliment_cute:int,
        compliment_list:int,
        compliment_note:int,
        compliment_plain:int,
        compliment_cool:int,
        compliment_funny:int,
        compliment_write:int,
        compliment_photos:int,
        type:chararray'
    );

review_data = 
	LOAD 'yelp_academic_data_review.json'
	USING JsonLoader(
		'review_id:chararray,
		user_id:chararray,
		business_id:chararray,
		stars:float,
		date:chararray,
		text:chararray,
		useful:int,
		funny:int,
		cool:int,
		type:chararray'
    );

user = FOREACH user_data GENERATE user_id, name, review_count;
business = FOREACH business_data GENERATE business_id, categories;
review = FOREACH review_data GENERATE business_id, user_id, stars;

user_sorted = ORDER user BY review_count DESC;
top_10_users = LIMIT user_sorted 10;

user_review_join = JOIN top_10_users BY user_id, review BY user_id;
user_review_trim = FOREACH user_review_join GENERATE top_10_users::user_id AS user_id, name, stars, business_id;

user_review_business_join = JOIN user_review_trim BY business_id, business BY business_id;

data = FOREACH user_review_business_join GENERATE user_id, name, FLATTEN(categories) AS category, stars;

grouped = GROUP data BY (user_id, name, category);
result = FOREACH grouped GENERATE FLATTEN(group), AVG(data.stars) AS AVG_STARS;

STORE result INTO 'yelp/answer42pig' USING PigStorage(',');